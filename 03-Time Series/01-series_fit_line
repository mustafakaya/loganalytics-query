// Applies linear regression on a series, returning multiple columns.

// rsquare: r-square is a standard measure of the fit quality. It's a number in the range [0-1], 
// where 1 - is the best possible fit, and 0 means the data is totally unordered and do not fit any line
// slope: slope of the approximated line (this is a from y=ax+b)
// variance: variance of the input data
// rvariance: residual variance which is the variance between the input data values the approximated ones.
// interception: interception of the approximated line (this is b from y=ax+b)
// line_fit: numerical array holding a series of values of the best fitted line. 
// The series length is equal to the length of the input array. It is mainly used for charting.

Perf
| where TimeGenerated > ago(1d)
| where CounterName == "Used Memory MBytes" 
| where CounterValue > 0
| make-series TotalMemoryUsed=sum(CounterValue) default=0 
           on TimeGenerated 
           in range(ago(1d), now(), 1h) 
           by Computer
| extend (RSquare,Slope,Variance,RVariance,Interception,LineFit)=series_fit_line(TotalMemoryUsed)
| render timechart 
